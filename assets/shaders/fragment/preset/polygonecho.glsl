precision highp float;
precision highp int;

uniform sampler2D tDiffuse;

uniform vec3 Color;
uniform float Opacity;
uniform int N;
uniform float StarInnerRadius;
uniform float Size;
uniform float Size2;
uniform float Width;
uniform vec2 Position;
uniform float Rotation;
uniform float Repeat;
uniform float Margin;
uniform float RotationStep;
uniform float StripeEnabled;
uniform float StripeWidth;
uniform float StripeMargin;
uniform float StripeOffset;
uniform float StripeRotation;

varying vec2 vUvScaled;

#define MAX_ITERATIONS 100
#define PI 3.14159265359

int maxInt(int a, int b) {
    return a > b ? a : b;
}

int minInt(int a, int b) {
    return a < b ? a : b;
}

vec4 blendColors(vec4 baseColor, vec4 overlayColor) {
    float alpha = overlayColor.a;
    return mix(baseColor, overlayColor, alpha);
}

vec2 rotate(vec2 uv, float angle) {
    float cosR = cos(angle);
    float sinR = sin(angle);
    vec2 rotatedUv;
    rotatedUv.x = uv.x * cosR - uv.y * sinR;
    rotatedUv.y = uv.x * sinR + uv.y * cosR;
    return rotatedUv;
}

float sdSegment(vec2 p, vec2 a, vec2 b) {
    vec2 pa = p - a;
    vec2 ba = b - a;
    float h = clamp(dot(pa, ba) / dot(ba, ba), 0.0, 1.0);
    return length(pa - ba * h);
}

float polygonDistance(vec2 point, int sides, float radius, float rotationRad, float innerRadius) {
    if (sides == 0) {
        return length(point) - radius;
    }

    if (innerRadius != 0.0) {
        int numPoints = sides * 2;
        float segmentAngle = 2.0 * PI / float(numPoints);

        float minDist = 1e10;
        bool inside = false;

        for (int i = 0; i < MAX_ITERATIONS; i++) {
            if (i >= numPoints) break;

            float angle1 = rotationRad - PI / 2.0 + segmentAngle * float(i);
            float r1 = (i / 2 * 2 == i) ? radius : radius * innerRadius;
            vec2 v1 = vec2(cos(angle1), sin(angle1)) * r1;

            int nextI = i + 1;
            if (nextI >= numPoints) nextI = 0;
            float angle2 = rotationRad - PI / 2.0 + segmentAngle * float(nextI);
            float r2 = (nextI / 2 * 2 == nextI) ? radius : radius * innerRadius;
            vec2 v2 = vec2(cos(angle2), sin(angle2)) * r2;

            float d = sdSegment(point, v1, v2);
            minDist = min(minDist, d);

            if ((v1.y > point.y) != (v2.y > point.y)) {
                float intersectX = (v2.x - v1.x) * (point.y - v1.y) / (v2.y - v1.y) + v1.x;
                if (point.x < intersectX) {
                    inside = !inside;
                }
            }
        }

        return inside ? -minDist : minDist;
    } else {
        float angle = rotationRad + atan(point.y, point.x) - PI / 2.0;
        float segmentAngle = 2.0 * PI / float(sides);
        float distanceToEdge = radius * cos(segmentAngle / 2.0) / cos(mod(angle, segmentAngle) - segmentAngle / 2.0);
        return length(point) - distanceToEdge;
    }
}

void main() {
    vec2 uv = vUvScaled;
    vec4 texel = texture2D(tDiffuse, uv);

    vec2 adjustedUV = uv - vec2(0.5);
    adjustedUV.x *= 16.0 / 9.0;

    vec2 rotatedUvForStripe = rotate(adjustedUV, StripeRotation * PI / 180.0);

    float size = (Size + Size2) * 0.1;
    float margin = Margin * 0.1;
    float width = Width * 0.1;
    vec2 position = Position * vec2(4.0 / 45.0, 0.05);

    vec2 pos = adjustedUV - position;
    float dist = length(pos);

    bool isInRing = false;

    int startI = 0;
    int count = 1;

    if (Repeat > 0.0) {
        count = minInt(int(Repeat), MAX_ITERATIONS);
    } else if (abs(margin) > 0.0001) {
        float safetyMargin = max(width * 2.0, 1.0);

        if (margin > 0.0) {
            float minIf = (dist - size - width * 0.5 - safetyMargin) / margin;
            startI = maxInt(0, int(floor(minIf)));

            float maxIf = (dist - size + width * 0.5 + safetyMargin) / margin;
            int endI = int(ceil(maxIf)) + 1;

            count = maxInt(1, minInt(MAX_ITERATIONS, endI - startI));
        } else {
            float minIf = (dist + width * 0.5 + safetyMargin - size) / margin;
            startI = maxInt(0, int(floor(minIf)));

            float maxIf = (dist - width * 0.5 - safetyMargin - size) / margin;
            int endI = int(ceil(maxIf)) + 1;

            float maxIForPositive = (-size - width * 0.5) / margin;
            endI = minInt(endI, int(floor(maxIForPositive)) + 1);

            count = maxInt(1, minInt(MAX_ITERATIONS, endI - startI));
        }
    }

    for (int j = 0; j < MAX_ITERATIONS; j++) {
        if (j >= count) break;

        int i = startI + j;

        float currentRotation = (Rotation + RotationStep * float(i)) * PI / 180.0;

        float currentSize = size + margin * float(i);
        float outSize = currentSize + width / 2.0;
        float inSize = currentSize - width / 2.0;

        if (outSize <= 0.0) continue;

        float distOuter = polygonDistance(pos, N, outSize, currentRotation, StarInnerRadius);

        if (inSize <= 0.0) {
            if (distOuter < 0.0) {
                isInRing = true;
                break;
            }
        } else {
            float distInner = polygonDistance(pos, N, inSize, currentRotation, StarInnerRadius);
            if (distOuter < 0.0 && distInner > 0.0) {
                isInRing = true;
                break;
            }
        }
    }

    if (isInRing) {
        if (StripeEnabled > 0.5) {
            float newStripeOffset = StripeOffset + StripeWidth * StripeMargin / 2.0;
            bool showStripe = fract((rotatedUvForStripe.y + newStripeOffset / 100.0) / (StripeWidth / 100.0)) < StripeMargin;

            if (showStripe) {
                vec4 shapeColor = vec4(Color, Opacity);
                gl_FragColor = blendColors(texel, shapeColor);
            } else {
                gl_FragColor = texel;
            }
        } else {
            vec4 shapeColor = vec4(Color, Opacity);
            gl_FragColor = blendColors(texel, shapeColor);
        }
    } else {
        gl_FragColor = texel;
    }
}
